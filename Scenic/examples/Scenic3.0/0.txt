#################################
# DESCRIPTION                   #
#################################

description =  "There is an adversary vehicle in front of the ego vehicle parked on the sidewalk curb. When ego vehicle approaches, adversary vehicle starts moving and cuts into ego vehicle's lane."

#################################
# MAP AND MODEL                 #
#################################

param map = localPath('../../../tests/formats/opendrive/maps/CARLA/Town05.xodr')
param carla_map = 'Town05'
model scenic.simulators.carla.model

#################################
# CONSTANTS                     #
#################################

MODEL = 'vehicle.mini.cooper_s_2021'

param EGO_SPEED = Range(3, 5)
param EGO_BRAKE = Range(0.5, 1.0)

ADV_INIT_DIST = [20,30]
CUT_IN_DIST = Range(15, 20)
PARK_OFFSET = Range(1, 3)
param ADV_SPEED = Range(7, 10)

SAFE_DIST = 8
INIT_DIST = 50
TERM_DIST = 50

#################################
# AGENT BEHAVIORS               #
#################################

behavior AdvBehavior(laneToFollow):
    while (distance from self to ego) > CUT_IN_DIST:
        wait
    do FollowLaneBehavior(laneToFollow=laneToFollow,target_speed=globalParameters.ADV_SPEED)

behavior EgoBehavior():
    try:
        do FollowLaneBehavior(target_speed=globalParameters.EGO_SPEED)
    interrupt when withinDistanceToAnyObjs(self,SAFE_DIST):
        take SetBrakeAction(globalParameters.EGO_BRAKE)

#################################
# SPATIAL RELATIONS             #
#################################

egoInitLane = Uniform(*network.lanes)
egoSpawnPt = new OrientedPoint in egoInitLane.centerline

parkAngle = Uniform(-1,1)*Range(10,20) deg

#################################
# SCENARIO SPECIFICATION        #
#################################

ego = new Car at egoSpawnPt,
        with blueprint MODEL,
        with behavior EgoBehavior()
        
adv = new Car left of ((new OrientedPoint on visible curb) offset by PARK_OFFSET @ 0), facing parkAngle relative to ego.heading,
    with blueprint MODEL,
    with behavior AdvBehavior(egoInitLane),
    with regionContainedIn None

require (angle from ego to adv) - ego.heading < 0 deg
require ADV_INIT_DIST[0] < (distance from ego to adv) < ADV_INIT_DIST[1]

require (distance to intersection) > INIT_DIST
terminate when (distance to egoSpawnPt) > TERM_DIST
